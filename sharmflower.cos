inst
reps 15
	new: simp 2 4 1058 "sharmflower" 6 0 140
	attr 192
	bhvr 11
	elas 0
*OV00 - Plant state: switches between growing UP and growing DOWN
	setv ov00 0
*OV99 - an age counter, counts the amount of time spent in a mature state.
	setv ov99 0
	tick rand 200 1000
*C3 norn meso
	doif tmvt rand 9056 10540 2166 eq 1
		mvto rand 9056 10540 2166
	endi
repe


*push script
scrp 2 4 1058 1
	targ from
	stim writ from 84 1
	targ ownr
endm

*Pull script
scrp 2 4 1058 2
	stim writ from 84 1
	targ ownr
endm

*timer script
scrp 2 4 1058 9
*Grow UP
	doif ov00 eq 0
		doif pose lt 5
			setv va00 pose
			addv va00 1
			pose va00
		else
*remain in a mature pose		
			addv ov99 1
		endi
*you've been mature for too long now, prepare to become immature.	
		doif ov99 eq 50
			setv ov00 1
		endi
	endi
*grow DOWN	
	doif ov00 eq 1
		doif pose gt 0
			setv va00 pose
			subv va00 1
			pose va00
		else
			setv ov99 0
			setv ov00 0
		endi
	endi
endm

*Hit script
scrp 2 4 1058 3
	targ from
	stim writ from 84 1
	targ ownr
*the plant grows DOWN if it is hit.	
	doif pose gt 0
		setv va00 pose
		subv va00 1
		pose va00
	endi
endm

*REMOVE ME
rscr
enum 2 4 1058
	kill targ
next
scrx 2 4 1058 1
scrx 2 4 1058 2
scrx 2 4 1058 3
scrx 2 4 1058 9